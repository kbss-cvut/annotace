plugins {
    id 'org.springframework.boot' version '2.5.3' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
}

group 'cz.cvut.kbss'
description 'Text analysis for Czech language and annotation recommendation service'
version '0.0.1'

def revision = 'git rev-list --count HEAD'.execute().text.trim()
def hash = 'git rev-parse --short HEAD'.execute().text.trim()
version = "0.0.1.r${revision}.${hash}";

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    compileJava {
        sourceCompatibility = '11'
        targetCompatibility = '11'
    }

    test {
        useJUnitPlatform()
    }

    group parent.group
    version parent.version

    repositories {
        jcenter()
        maven {
            name = 'kbss-private'
            url = uri('https://kbss.felk.cvut.cz/m2repo-private')
        }
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.30'
        implementation 'ch.qos.logback:logback-classic:1.2.3'
        compileOnly 'org.projectlombok:lombok:1.18.20'

        annotationProcessor "org.projectlombok:lombok:1.18.20"

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }
}
